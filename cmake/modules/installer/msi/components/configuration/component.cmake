get_host_os(HOST_OS)
set_os_ext(${HOST_OS} EXT)
set(CMAKE_SOURCE_DIR_PATH ${CMAKE_SOURCE_DIR})
set(COMMON_VARIABLES 
    PROJECT_NAME
    APP_NAME
    APP_TAG
    VERSION_SEMVER
    CHANGELOG_PATH
    PROJECT_GIT
    PROJECT_MAINTANER
    PROJECT_ORGANIZATION
    RELEASE_DATE_MSI
    PROJECT_HOMEPAGE
    PROJECT_DESCRIPTION
    LINUX_APP_BINARY_DIR
    LINUX_APP_DATA_DATABASE_DIR
    CMAKE_SOURCE_DIR_PATH
    DIST_ROOT_DIR
    GOOS
    EXT
    )

file(GLOB_RECURSE MSI_FILES "${CMAKE_CURRENT_LIST_DIR}/msi-files/*")

set(MSI_CONFIG_VARIABLES ${COMMON_VARIABLES})
foreach(MSIARCH ${ALL_MSIARCH_LIST_WINDOWS})
    map_msiarch_to_arch_all(${MSIARCH} APP_ARCH)
    
    set(MSI_ROOT_DIR ${CMAKE_SOURCE_DIR}/${DIST_ROOT_DIR}/${APP_TAG}/${HOST_OS}/ins/${MSI_PACKAGE_NAME}/${APP_ARCH})
    set(MSI_PAYLOAD_DIR ${MSI_ROOT_DIR}/${APP_NAME})
    set(MSI_OUTPUT_DIR ${MSI_ROOT_DIR}/output)
    set(MSI_CONF_DIR ${MSI_ROOT_DIR}/${APP_NAME})

    set(BIN_OUTPUT_FULL_PATH ${MSI_OUTPUT_DIR}/${APP_NAME}/${DIST_ROOT_DIR}/${APP_TAG}/${HOST_OS}/bin/${APP_ARCH}/${APP_NAME}${EXT})

    list(APPEND MSI_CONFIG_VARIABLES APP_ARCH)
    list(APPEND MSI_CONFIG_VARIABLES MSIARCH)
    list(APPEND MSI_CONFIG_VARIABLES BIN_OUTPUT_FULL_PATH)

    set(MSI_FILE_NAMES "")
    foreach(MSIFILE ${MSI_FILES})
        file(RELATIVE_PATH REL_FILE_PATH "${CMAKE_CURRENT_LIST_DIR}/msi-files" ${MSIFILE})

        set(CONFIG_FILE_PATH "${MSI_CONF_DIR}/${REL_FILE_PATH}")
        list(APPEND MSI_FILE_NAMES ${CONFIG_FILE_PATH})
        list(APPEND MSI_CONFIG_VARIABLES CONFIG_FILE_PATH)

        var_list_to_cmake_args(VARIABLES_TO_PASS "${MSI_CONFIG_VARIABLES}")
        add_custom_command(
            OUTPUT ${CONFIG_FILE_PATH}
            COMMAND ${CMAKE_COMMAND} -E echo "Generating ${MSIFILE} file..."
            COMMAND ${CMAKE_COMMAND} ${VARIABLES_TO_PASS} -P "${MSIFILE}"
            COMMENT "Generating ${MSIFILE} to ${CONFIG_FILE_PATH}"
        )
    endforeach()

    add_custom_target(build.msi.package.${APP_ARCH}.configuration DEPENDS check_env_for_msi_packing ${MSI_FILE_NAMES})
endforeach()